apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

ext {
    supportLibVersion = '27.1.1'
}

android {
    compileSdkVersion 27

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def build = versionProps['VERSION_BUILD'].toInteger() + 1

        versionProps['VERSION_BUILD']=build.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.amazmod.service"
            minSdkVersion 21
            //noinspection ExpiredTargetSdkVersion
            targetSdkVersion 21
            versionCode build
            versionName "${build}"
        }
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation project(':transport')
    implementation project(':models')
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation 'com.mikepenz:iconics-core:3.0.3@aar'
    implementation 'com.mikepenz:iconics-views:3.0.3@aar'
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'
    implementation 'xiaofei.library:hermes-eventbus:0.3.0'
    implementation 'com.github.marlonlom:timeago:3.0.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'com.github.tbouron.shakedetector:library:1.0.0@aar'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation files('../libs/SpringboardPluginLib.jar')
    implementation files('../libs/huami_libs.jar')
    implementation files('../libs/WearServices_libs.jar')
    compileOnly 'com.google.android.wearable:wearable:2.3.0'
    implementation 'com.google.android.support:wearable:2.3.0'
    implementation "com.android.support:support-emoji:$supportLibVersion"
    implementation "com.android.support:support-emoji-appcompat:$supportLibVersion"
    implementation "com.android.support:support-emoji-bundled:$supportLibVersion"
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:4.2.4"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:4.2.4"
    implementation "com.github.Raizlabs.DBFlow:dbflow:4.2.4"
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'org.tinylog:tinylog-api:2.0.0-M4.2'
    implementation 'org.tinylog:tinylog-impl:2.0.0-M4.2'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '27.1.1'
            }
        }

    }
}