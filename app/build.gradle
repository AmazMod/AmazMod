apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt' // required for Kotlin

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'com.edotassi.amazmod'
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 228
        versionName "1.2.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        if (branch == "dev") {
            versionNameSuffix "-" + branch
            setProperty("archivesBaseName", "AmazMod-$project.name")
        } else {
            setProperty("archivesBaseName", "AmazMod-$project.name-$versionCode")
        }
    }
    dataBinding {
        enabled = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        all {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            manifestPlaceholders = [enableCrashReporting: "true"]
            signingConfig signingConfigs.release
        }
        debug {
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
    }
    productFlavors {
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->

            def newApkName = ""
            def baseName = archivesBaseName
            def buildType = variant.buildType.name
            def date = new Date();
            def formattedDate = date.format('YYYYMMddHHmm', TimeZone.getTimeZone('UTC'))

            if (branch == "master") {
                if (buildType == "release") {
                    newApkName = "$baseName" + ".apk"
                } else if (buildType == "debug") {
                    newApkName = "$baseName-$buildType" + ".apk"
                }
            } else {
                newApkName = "$baseName-$buildType-$branch-$versionCode-$formattedDate" + ".apk"
            }

            outputFileName = new File(newApkName)
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation project(':transport')
    implementation project(':models')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.heinrichreimersoftware:material-intro:1.5.8'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation files('../libs/huami_libs.jar')
    implementation files('../libs/SpringboardPluginLib.jar')
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.mikepenz:iconics-core:3.0.4@aar'
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'
    implementation 'com.jakewharton:butterknife:10.1.0'
    kapt 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:${dbflow_version}"
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.pixplicity.easyprefs:library:1.9.0'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    implementation 'com.github.MFlisar:changelog:1.1.5'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.github.hedzr:android-file-chooser:master-SNAPSHOT'
    implementation 'com.github.matecode:Snacky:1.1.4'
    implementation 'com.github.tingyik90:snackprogressbar:5.0'
    implementation 'com.karumi:dexter:5.0.0'
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.mindorks.android:prdownloader:0.4.0'
    implementation 'org.mnode.ical4j:ical4j:2.2.3'
    implementation 'org.slf4j:slf4j-simple:1.7.25'
    implementation "org.tinylog:tinylog-api:${tinylog_version}"
    implementation "org.tinylog:tinylog-api-kotlin:${tinylog_version}"
    implementation "org.tinylog:tinylog-impl:${tinylog_version}"
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'commons-net:commons-net:3.6'
    implementation 'com.android.billingclient:billing:2.2.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.github.ByteHamster:SearchPreference:v2.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '$supportLibVersion'
            }
        }
    }
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}


def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
